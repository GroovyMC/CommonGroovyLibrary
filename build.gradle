plugins {
    id 'java'
    id 'groovy'
    id 'io.github.groovymc.modsdotgroovy' version '1.1.0'
    id 'com.matyrobbrt.mc.registrationutils' version '1.19-1.3.0'
}

registrationUtils {
    group 'io.github.groovymc.cgl.reg'
    projects {
        Common {}
        Quilt {}
        Forge {}
    }
}

group = 'io.github.groovymc.cgl'

modsDotGroovy {
    dslVersion = '1.1.3'
    platform 'multiloader'
}

subprojects {
    apply plugin: 'groovy'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    afterEvaluate {
        reg.configureSourcesJarTask('sourcesJar')
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : 'CommonGroovyLibrary-' + project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }
    
    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            name 'Quilt (Release)'
            url 'https://maven.quiltmc.org/repository/release/'
        }
        maven {
            name 'ModdingInquisition (Release)'
            url 'https://maven.moddinginquisition.org/releases/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }
    tasks.withType(GroovyCompile).configureEach {
        it.groovyOptions.fork = true
        it.groovyOptions.encoding = 'UTF-8'
        it.groovyOptions.optimizationOptions.indy = true
        it.exclude('mods.groovy')
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    configurations {
        testCompileOnly.extendsFrom(compileOnly)
    }

    dependencies {
        compileOnly groovyDep('stdlib')
        compileOnly groovyDep('ginq')
        compileOnly groovyDep('nio')
        compileOnly groovyDep('contracts')
        compileOnly groovyDep('macro')
        compileOnly groovyDep('macro-library')
        compileOnly groovyDep('templates')
    }
}

String groovyDep(String name) {
    return "org.apache.groovy:groovy${name == 'stdlib' ? '' : '-' + name}:${project.groovy_version}"
}