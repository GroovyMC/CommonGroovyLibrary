plugins {
    id 'maven-publish'
    id 'org.quiltmc.loom' version '0.12.+'
}

archivesBaseName = "cgl-${minecraft_version}"

repositories {
    maven {
        name = 'Forge / Mixin'
        url = 'https://maven.minecraftforge.net'
    }
}

sourceSets {
    transform {}
    extension {
        compileClasspath += transform.output
        runtimeClasspath += transform.output
    }
    main {
        java {
            srcDirs = []
        }
        compileClasspath += transform.output
        runtimeClasspath += transform.output
        compileClasspath += extension.output
        runtimeClasspath += extension.output
    }
}

configurations {
    extensionCompileOnly.extendsFrom(compileClasspath)
    annotationsCompileOnly.extendsFrom(compileClasspath)
    transformCompileOnly.extendsFrom(compileClasspath)

    transformOut
    extensionOut
}

loom {
    enableTransitiveAccessWideners = false
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
    interfaceInjection {
        enableDependencyInterfaceInjection = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    //DO NOT USE IN PROJECT
    //Provides necessary stuff for project to compile
    modCompileOnly "org.quiltmc:quilt-loader:${quilt_loader_version}"
    modCompileOnly 'com.github.javaparser:javaparser-core:3.24.4'

    implementation group:'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    extensionOut sourceSets.extension.output
    extensionOut sourceSets.transform.output
    transformOut sourceSets.transform.output
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}

configurations.configureEach {
    it.exclude module: 'sponge-mixin'
}

processResources {
    def buildProps = project.properties.clone()
    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
    exclude 'mods.groovy'
}

jar {
    from sourceSets.extension.output
    from sourceSets.transform.output
}

sourcesJar {
    from sourceSets.extension.allSource
    from sourceSets.transform.allSource
}

groovydoc {
    use = true
    docTitle = "CommonGroovyLibrary v${version} - Common"
    source sourceSets.extension.allGroovy
    source sourceSets.extension.allJava
    source sourceSets.transform.allGroovy
    source sourceSets.transform.allJava
}

tasks.register('groovydocJar', Jar) {
    it.classifier 'javadoc'
    it.from groovydoc.destinationDir
    it.dependsOn(groovydoc)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.groovymc.cgl'
            artifactId = "cgl-${minecraft_version}"
            version = project.version
            from components.java
            artifact groovydocJar
        }
    }
    repositories {
        maven {
            name = 'ModdingInquisitionMavenRepo'
            url = 'https://maven.moddinginquisition.org/releases'
            credentials {
                username = findProperty('inquisitionMavenUser') ?: ''
                password = findProperty('inquisitionMavenPassword') ?: ''
            }
        }
    }
}